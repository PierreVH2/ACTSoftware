# shared library version info:
# Three number version: "current", "revision", "age"
#
# Existing version:              C.R.A
# ----------------------------------------
# Backwards-incompatible change: C+1.0.0
# Feature added:                 C+1.0.A+1
# Bugfix:                        C.R+1.A
soversion = '0.0.0'

# Command-line options: installation prefix
AddOption('--prefix',
          dest='prefix',
          type='string',
          nargs=1,
          action='store',
          metavar='DIR',
	  default=GetLaunchDir(),
          help='installation prefix. Default is %r' % GetLaunchDir())

env = Environment(CCFLAGS=['-O3','-Wall'],
                  PREFIX=GetOption('prefix'),
                  SHLIBVERSION=soversion)

# Build library targets
sources = Glob('*.c')
shlib = env.SharedLibrary('sep', sources, LIBS=['m', 'c'],
                          CPPPATH='.', SHLIBVERSION=soversion)
lib = env.StaticLibrary('sep', sources, LIBS=['m', 'c'], CPPPATH='.')

# Install Target
# NOTE: shlibinstall is commented out because I can't get it to respect
#       Default. That is, it always gets built, even with just a 'scons'
#       invocation (no arguments).
# shlibinstall = env.InstallVersionedLib('$PREFIX/lib', shlib)
libinstall = env.Install('$PREFIX/lib', lib)
hinstall = env.Install('$PREFIX/include', 'sep.h')

env.Alias('install', [libinstall, hinstall])

env.Default(lib, shlib)
